/* [wxMaxima batch file version 1] [ DO NOT EDIT BY HAND! ]*/
/* [ Created with wxMaxima version 20.06.6 ] */
/* [wxMaxima: title   start ]
10 นาที สำหรับการแนะนำการใช้งานโปรแกรม (wx)Maxima เบื้องต้น

   [wxMaxima: title   end   ] */


/* [wxMaxima: comment start ]
ยินดีต้อนรับเข้าสู่การใช้งานโปรแกรม wxMaxima ในเอกสารแนะนำการใช้งานเบื้องต้นนี้
ท่านจะได้เรียนรู้เกี่ยวกับคำสั่งพื้นฐานของโปรแกรม wxMaxima และโปรแกรม Maxima 
ซึ่งโปรแกรม Maxima เป็นโปรแกรมระบบพีชคณิตเชิงคอมพิวเตอร์ (Computer Algebra System หรือเรียกย่อๆ ว่า CAS)
จะคล้ายๆกับโปรแกรม Mathematica  โปรแกรม Maple และโปรแกรมอื่นๆ  
อย่างไรก็ตามสำหรับโปรแกรม Maxima เป็นโปรแกรมประยุกต์แบบคำสั่งบรรทัดต่อบรรทัด ซึ่งอาจจะทำให้มีความยุ่งยากเล็กน้อยในการใช้งานเมื่อเปรียบกับโปรแกรมในลักษณะเดียวกันที่เพิ่งออกมาภายหลัง
ในที่นี้คือ โปรแกรม wxMaxima โดยโปรแกรมนี้จะมี GUI(Graphical User Interface) สำหรับโปรแกรม Maxima ที่ทำให้ง่ายกว่าต่อการใช้งานและเกิดความพึงพอใจมากมาย

มาเริ่มต้นการใช้งานโปรแกรมด้วยตัวอย่างการคำนวณอย่างง่าย ด้านล่างนี้เป็น
เป็นส่วนที่จะใส่คำสั่งเพื่อให้คำนวณการบวกเลขอย่างง่าย ให้วางเคอร์เซอร์ในบรรทัดด้านล่างนี้และกดปุ่ม SHIFT พร้อมกับกดปุ่ม ENTER เพื่อให้โปรแกรมประมวลผลลัพธ์ออกมา

   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
1 + 1;
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ถ้าไม่มีข้อความแสดงข้อผิดพลาดใดๆ เกิดขึ้น นั่นแสดงว่าโปรแกรม Maxima ได้ตั้งค่าการทำงานได้ถูกต้องแล้ว
ถ้าพบข้อความแสดงข้อผิดพลาดเกิดขึ้น ท่านควรตรวจสอบการตั้งค่าการทำงานของโปรแกรม wxMaxima หรือ
เข้าไปที่เว็บไซต์ของโปรแกรม wxMaxima (http://wxmaxima.sourceforge.net/) สำหรับศึกษาดูขั้นตอนแนะนำ
ในการติดตั้งโปรแกรม wxMaxima และโปรแกรม Maxima อย่างถูกต้อง

สมมติว่าท่านได้จัดการกับปัญหาข้างต้นไปแล้ว ทีนี้ลองมาทำการคำนวณอย่างอื่นดูบ้าง
(เหมือนเดิม โดยวางเคอร์เซอร์บนส่วนของบรรทัดคำสั่งด้านล่างนี้ แล้วให้กดปุ่ม SHIFT และกดปุ่ม ENTER พร้อมกัน
 เพื่อให้โปรแกรมแสดงการประมวลผลลัพธ์ออกมา)
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
5!;
% * 10;
%o1 * 100;
1 / 3;
1.0 / 3.0;
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ในส่วนของบรรทัดคำสั่งข้างต้น เป็นการสั่งให้โปรแกรม Maxima ประมวลผลคำสั่ง 5 บรรทัดคำสั่ง
แต่ละคำสั่งจะต้องจบคำสั่งด้วยเครื่องหมาย ";" หรือ "$"  ในกรณีที่คำสั่งจบด้วยเครื่องหมาย ";" โปรแกรม Maxima
จะแสดงผลลัพธ์ของบรรทัดคำสั่ง ในขณะที่ผลลัพธ์ของคำสั่งที่จบด้วยเคื่องหมาย "$" จะประมวลผลลัพธ์แต่ไม่แสดงให้เห็นบนหน้าจอ
เครื่องหมาย "$" จะมีประโยชน์เมื่อต้องประมวลผลคำสั่งของการคำนวณที่ยาวๆ 
จากข้างต้นจะสังเกตได้ว่าผลลัพธ์ของ "1/3" และ "1.0/3.0" จะแสดงผลแตกต่างกัน
นั่นก็เพราะว่า โปรแกรม Maxima ซึ่งจะไม่เหมือน packages ของเมทริกซ์เชิงตัวเลข (ดังเช่นในโปรแกรม Matlab  เป็นต้น)
โปรแกรมนี้จะพยายามแสดงผลลัพธ์ของการคำนวณให้มีความแม่นตรงที่สุด โปรแกรมจะไม่ประมวลผลนิพจน์ในลักษณะ
1/3 หรือ sqrt(2)  จนกว่าจะมีการร้องขอ  ในส่วนของคำสัง "1.0/3.0" เป็นการคำนวณตัวเลขที่มีจุดทศนิยม ดังนั้นโปรแกรม Maxima
จะไม่ละทิ้งการประมวลผลนิพจน์ 

อย่างไรก็ตาม เราสามารถที่จะสั่งให้โปรแกรม Maxima ว่าเราต้องการให้โปรแกรมประมวลผลค่าประมาณเลขทศนิยมของนิพจน์ได้
ประมวลผลบรรทัดคำสั่งด้านล่างและสังเกตผลลัพธ์ที่เกิดขึ้น


   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
sqrt(2 * %pi);
float(%);
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ในบรรทัดคำสั่ง "float(%);" เราจะใช้เครื่องหมาย "%" เครื่องหมายนี้จะแทนผลลัพธ์ของบรรทัดคำสั่งล่าสุดเสมอ
เครื่องหมายที่มีตัวเลขอย่างเช่น "%o1", "%o2" ... จะแทนผลลัพธ์ของบรรทัดคำสั่งต่างๆ เมื่อถูกประมวลผล
นอกจากนี้ เราสามารถเก็บค่าไว้ไม่เพียงแต่จำนวนเท่านั้น แต่ยังสามารถเก็บค่าตลอดทั้งนิพจน์ ไว้ในรูปของตัวแปร 
ใช้รูปแบบ "variable_name: value;" เพื่อเก็บค่าไว้ในตัวแปร "variable_name"
ประมวลผลบรรทัดคำสั่งด้านล่างนี้และสังเกตผลลัพธ์ที่ได้  

   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
radius: 10 $
height: 100 $
area: %pi * radius^2;
volume: area * height;
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
เราสามารถประมวลผลเชิงตัวเลขของผลลัพธ์ล่าสุดได้ดังนี้
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
float(%);
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
มาถึงตอนนี้ เราได้ใช้โปรแกรม Maxima ในการคำนวณอย่างง่าย เท่านั้น 
ต่อไปนี้ลองดูบางการคำนวณที่ทำไม่ได้ในเครื่องคิดเลข นั่นคือการอินทิเกรตแบบไม่จำกัดเขตและแบบจำกัดเขต ดังนี้

   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
integrate( sin(x), x);
integrate( sin(x), x, 0, %pi);
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ต่อไปลองมานิยามฟังก์ชัน แล้วหาค่าของฟังก์ชันและอินทิเกรตฟังก์ชัน ดังตัวอย่างต่อไปนี้
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
f(x) := x^2 + a$
f(5);
f(5), a = -5;
integrate( f(var), var );
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
บางครั้งโปรแกรม Maxima จำเป็นต้องทราบข้อมูลเพิ่มเติมในการประมวลผลนิพจน์
และถามคำถามเพิ่มเติม เคอร์เซอร์จะปรากฏขึ้นมาอัตโนมัติ เราจำเป็นที่จะต้องตอบคำถามนั้น
ถ้าไม่ได้ ให้พิมพ์คำตอบใต้เซลล์และส่งคำตอบไปยังโปรแกรม Maxima ด้วยการกดปุ่ม SHIFT และปุ่ม ENTER
หมายเหตุ: การตอบคำตอบ "positive;" ท่านสามารถพิมพ์แทนด้วย "p" เท่านั้น  
ตัวอย่างการอินทิเกรตด้านล่างนี้จะเป็นตัวอย่างที่จะมีการถามคำถามเพิ่มเติม:

   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
integrate( 1 / (x^2 + a), x);
/* [wxMaxima: input   end   ] */
/* [wxMaxima: question  start ] */
<math><st>Is </st><mi>a</mi><st> positive or negative?</st></math>
/* [wxMaxima: question  end   ] */
/* [wxMaxima: answer  start ] */
p;
/* [wxMaxima: answer  end   ] */


/* [wxMaxima: comment start ]
นอกจากนี้ เรายังสามารถบอกให้โปรแกรม Maxima ทราบก่อนล่วงหน้าได้ โดยใช้ฟังก์ชันคำสั่ง "assume"
ในการคืนสภาพเดิมจะใช้คำสั่ง "forget"
(หมายเหตุ: ในการใช้ตัวช่วยเพื่ออธิบายฟังก์คำสั่งใดๆ ทำได้โดยการคลิ๊กที่ชื่อฟังก์ชันนั้นแล้วกด F1 ทั้งนี้ในบางเครื่องอาจจะต้องกดปุ่ม Fn และ กดปุ่ม F1 พร้อมกัน ลองทำดูนะครับ)

   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
assume(a > 0)$
integrate( 1 / (x^2 + a), x);
forget(a > 0)$
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ถึงตอนนี้ท่านได้เรียนรู้พื้นฐานการใช้งานของโปรแกรมแล้ว ถึงเวลาที่จะพิจารณาบางปัญหาทั่วไปทางคณิตศาสตร์!
ข้อควรจำ: ถ้าท่านต้องการที่จะทราบรายละเอียดเพิ่มเติมเกี่ยวกับฟังก์ชันพิเศษ ให้คลิ๊กที่ชื่อฟังก์ชันนั้นแล้วกด F1 ทั้งนี้ในบางเครื่องอาจจะต้องกดปุ่ม Fn และ กดปุ่ม F1 พร้อมกัน 

การแก้สมการโดยการใช้คำสั่ง solve:
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
solve(a*x^2 + b*x + c = 0, x);
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ในพีชคณิตเชิงเส้น ใช้คำสั่ง "matrix" เพื่อสร้างเมทริกซ์  สมาชิกของเมทริกซ์สามารถเป็นนิพจน์ที่ไม่ใช่ตัวเลขได้
เราจะใช้คำสั่ง "invert" เพื่อคำนวณหาเมทริกซ์ผกผันและใช้ "." สำหรับการคูณกันของเมทริกซ์
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
A: matrix([1,-1],
          [1,sin(c)]);
B: invert(A);

A.B;
ratsimp(A.B);
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ในบรรทัดคำสั่งสุดท้าย เราใช้คำสั่ง "ratsimp" เพื่อแสดงผลลัพธ์ของ "A.B" ในรูปอย่างง่าย
โปรแกรม Maxima มีฟังก์ชันคำสั่งการแสดงในรูปอย่างง่ายมากมายที่แตกต่างกัน ขึ้นอยู่กับรูปแบบ
การแสดงผลอย่างง่ายที่ท่านต้องการ การแสดงในรูปอย่างง่ายเป็นหัวข้อที่ซับซ้อนและถ้าท่านไม่ทราบเกี่ยวกับการแสดงในรูปอย่างง่าย
ให้ใช้คำสั่ง "ratsimp" ซึ่งอาจจะเป็นคำสั่งการแสดงในรูปแบบอย่าง่ายที่ดีที่สุดของท่าน

ต่อมาลองสร้างกราฟแบบ 2 มิติ และกราฟแบบ 3 มิติดังตัวอย่างนี้
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
wxplot2d([sin(x), cos(x)], [x,0, 2*%pi]);
wxplot3d( exp(-x^2 - y^2), [x,-2,2],[y,-2,2]);
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
เรามาลองหาอนุพันธ์โดยใช้ฟังก์ชันคำสั่ง "diff" กันดู
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
f(x) := x^2 $
diff(f(x), x);
g(y) := sin(y)$
g(f(x));
diff( g(f(x)) , x);
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ใช่แล้ว โปรแกรม Maxima สามารถทำการคำนวณเกี่ยวกับกฎลูกโซ่ได้

และสำหรับตัวอย่างสุดท้ายนี้ เราลองมาแก้ปัญหาสมการเชิงอนุพันธ์สามัญอันดับสอง:
y''(t) + omega^2 * y(t) = 0
   [wxMaxima: comment end   ] */


/* [wxMaxima: input   start ] */
assume(omega > 0);
ode2( 'diff(y, t, 2) + omega^2 * y = 0, y, t );

ic2(%, t = 0, y = amp, 'diff(y,t) = 0 );
/* [wxMaxima: input   end   ] */


/* [wxMaxima: comment start ]
ตอนนี้ท่านควรที่จะเริ่มการสำรวจโปรแกรม (wx)Maxima ของท่านเอง
อย่าลืมที่จะกดปุ่ม F1 บ่อยๆ ถ้าท่านต้องการให้โปรแกรม Maxima ช่วยท่านแก้ปัญหาทางคณิตศาสตร์ของท่าน!


ขอให้มีความสุขกับการใช้โปรแกรม Maxima และ wxMaxima นะครับ!
   [wxMaxima: comment end   ] */



/* Old versions of Maxima abort on loading files that end in a comment. */
"Created with wxMaxima 20.06.6"$
